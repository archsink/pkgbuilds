# Maintainer: Holzhaus <jholthuis@mixxx.org>
# Contributor: Gimmeapill <gimmeapill@gmail.com>
# Contributor: regreddit <nik.martin@gmail.com>

pkgname=mixxx-git
pkgver=r9626
pkgrel=1
pkgdesc="Digital DJ mixing software. Git master branch (development/alpha)."
# TODO: Re-add aarch64 once we figure out a performant way to build it
# See https://github.com/archsink/pkgbuilds/pull/12 for details
arch=('i686' 'pentium4' 'x86_64')
url="https://mixxx.org/"
license=('GPL2')
groups=('pro-audio')
depends=(
  'chromaprint'
  'flac'
  'hidapi'
  'lame'
  'libebur128'
  'libid3tag'
  'libmad'
  'libmodplug'
  'libmp4v2'
  'libshout'
  'libsndfile'
  'libtheora'
  'libusb'
  'lilv'
  'microsoft-gsl'
  'opusfile'
  'portaudio'
  'portmidi'
  'protobuf'
  'qt6-declarative'
  'qt6-5compat'
  'qt6-svg'
  'qt6-translations'
  'qtkeychain-qt6'
  'rubberband'
  'soundtouch'
  'taglib'
  'ttf-opensans'
  'ttf-ubuntu-font-family'
  'upower'
  'wavpack'
)
makedepends=('git' 'lv2' 'qt6-tools' 'cmake' 'ninja' 'gtest' 'benchmark')
provides=('mixxx')
conflicts=('mixxx')
source=("${pkgname%-*}::git+https://github.com/mixxxdj/mixxx.git"
        'mixxx.install')
sha256sums=('SKIP'
            '8f2db8c423cce9685df8ce010dc1dfbf48d0961e7a453d40c495220bf3a272ec')
install='mixxx.install'

pkgver() {
  cd "$srcdir/${pkgname%-*}"
  echo "r$(git log --pretty=oneline --first-parent | wc -l)"
}

prepare() {
  mkdir -p "$srcdir/${pkgname%-*}/build"
  cmake -S $srcdir/${pkgname%-*} -G Ninja -B $srcdir/${pkgname%-*}/build \
  -DCMAKE_INSTALL_PREFIX=/usr \
  -DINSTALL_USER_UDEV_RULES=OFF \
  -DCMAKE_BUILD_TYPE=RelWithDebInfo \
  -DOPTIMIZE=native \
  -DQT6=ON \
  -DBULK=ON \
  -DFAAD=ON \
  -DLILV=ON \
  -DFFMPEG=ON \
  -DKEYFINDER=ON \
  -DMAD=ON \
  -DMODPLUG=ON \
  -DOPUS=ON \
  -DQTKEYCHAIN=ON \
  -DWAVPACK=ON
}

build() {
  cmake --build "$srcdir/${pkgname%-*}/build" --parallel "$(nproc)" --target mixxx
}

check() {
  cmake --build "$srcdir/${pkgname%-*}/build" --parallel "$(nproc)" --target mixxx-test
  ctest --test-dir "$srcdir/${pkgname%-*}/build" --parallel "$(nproc)" --output-on-failure
}

package() {
  mkdir -p "$pkgdir/usr/lib/udev/rules.d/"
  install -Dm644 "$srcdir/${pkgname%-*}/res/linux/mixxx-usb-uaccess.rules" "$pkgdir/usr/lib/udev/rules.d/99-mixxx-usb-uaccess.rules"
  DESTDIR="$pkgdir" cmake --install "$srcdir/${pkgname%-*}/build"
}
