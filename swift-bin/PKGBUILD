# Maintainer: Susurri <susurrus dot silent at gmail dot com>
# Contributor: Miten <git dot pub at icloud dot com>
# Contributor: Ryan Gonzalez <rymg19 at gmail dot com>
# Contributor: Frederic Bezies <fredbezies at gmail dot com>, youngunix <>

pkgname=swift-bin
pkgver=5.9.1
pkgrel=2
pkgdesc="Binary builds of the Swift programming language"
arch=('x86_64' 'aarch64')
url="https://www.swift.org/"
license=('apache')
makedepends=('patchelf')
depends=('libutil-linux' 'libxml2' 'ncurses')
optdepends=('python36: required for REPL')
options=('!strip')
provides=('swift-language')
replaces=('swift-language-bin')

_ubuntumajor=22
_ubuntuminor=04

source_x86_64=("https://download.swift.org/swift-$pkgver-release/ubuntu$_ubuntumajor$_ubuntuminor/swift-$pkgver-RELEASE/swift-$pkgver-RELEASE-ubuntu$_ubuntumajor.$_ubuntuminor.tar.gz")
source_aarch64=("https://download.swift.org/swift-$pkgver-release/ubuntu$_ubuntumajor$_ubuntuminor-aarch64/swift-$pkgver-RELEASE/swift-$pkgver-RELEASE-ubuntu$_ubuntumajor.$_ubuntuminor-aarch64.tar.gz")

sha256sums_x86_64=('ea2fe1190a9cb8abed8c5e7b94223a06a23c7dc8cd498850a1c79b8a87e7c251')
sha256sums_aarch64=('8331bc3f50dbc484d1e4c939aad8399b567fced63d59167c7c2169ee5ff7168f')

prepare() {
  case "$CARCH" in
    x86_64) _suffix="";;
    *) _suffix="-$CARCH";;
  esac

  # Patch the ncurses dependency to the one provided by the ncurses package
  # TODO: This currently emits a few warnings for non-ELF executables, find a more elegant way to deal with them
  find "${srcdir}/swift-$pkgver-RELEASE-ubuntu$_ubuntumajor.$_ubuntuminor$_suffix" -type f -executable -exec \
    patchelf --replace-needed libncurses.so.6 libncursesw.so.6 {} \;
}

package() {
  case "$CARCH" in
    x86_64) _suffix="";;
    *) _suffix="-$CARCH";;
  esac

  mkdir -p "${pkgdir}/usr/lib/swift"
  cp -Ppr "${srcdir}/swift-$pkgver-RELEASE-ubuntu$_ubuntumajor.$_ubuntuminor$_suffix"/usr/* "${pkgdir}/usr/lib/swift"

  # Symlink the desired binaries to /usr/bin
  mkdir -p "${pkgdir}/usr/bin"
  for bin in sourcekit-lsp swift swiftc; do
    ln -s "/usr/lib/swift/bin/$bin" "${pkgdir}/usr/bin/$bin"
  done

  install -dm755 "${pkgdir}/etc/ld.so.conf.d"
  echo '/usr/lib/swift/lib/swift/linux' >> "${pkgdir}/etc/ld.so.conf.d/swift.conf"
}
