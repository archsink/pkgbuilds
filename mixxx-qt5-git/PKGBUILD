# Maintainer: Holzhaus <jholthuis@mixxx.org>
# Contributor: Gimmeapill <gimmeapill@gmail.com>
# Contributor: regreddit <nik.martin@gmail.com>

_pkgname=mixxx
pkgname=$_pkgname-qt5-git
pkgver=r8881
pkgrel=1
pkgdesc="Digital DJ mixing software. Git master branch built with Qt 5 (development/alpha)."
# TODO: Re-add aarch64 once we figure out a performant way to build it
# See https://github.com/fwcd/arch-repo/pull/12 for details
arch=('i686' 'pentium4' 'x86_64')
url="https://mixxx.org/"
license=('GPL2')
groups=('pro-audio')
depends=(
  'chromaprint'
  'flac'
  'hidapi'
  'lame'
  'libebur128'
  'libid3tag'
  'libmad'
  'libmodplug'
  'libmp4v2'
  'libshout'
  'libsndfile'
  'libtheora'
  'libusb'
  'lilv'
  'microsoft-gsl'
  'opusfile'
  'portaudio'
  'portmidi'
  'protobuf'
  'qt5-declarative'
  'qt5-quickcontrols'
  'qt5-quickcontrols2'
  'qt5-svg'
  'qt5-translations'
  'qt5-x11extras'
  'qtkeychain'
  'rubberband'
  'soundtouch'
  'taglib'
  'ttf-opensans'
  'ttf-ubuntu-font-family'
  'upower'
  'wavpack'
)
makedepends=('git' 'lv2' 'qt5-tools' 'cmake' 'ninja' 'gtest' 'benchmark')
provides=("$_pkgname")
conflicts=("$_pkgname")
source=("$_pkgname::git+https://github.com/mixxxdj/$_pkgname.git"
        "$_pkgname.install")
sha256sums=('SKIP'
            '8f2db8c423cce9685df8ce010dc1dfbf48d0961e7a453d40c495220bf3a272ec')
install="$_pkgname.install"

pkgver() {
  cd "$srcdir/$_pkgname"
  echo "r$(git log --pretty=oneline --first-parent | wc -l)"
}

prepare() {
  mkdir -p "$srcdir/$_pkgname/build"
  cmake -S $srcdir/$_pkgname -G Ninja -B $srcdir/$_pkgname/build \
  -DCMAKE_INSTALL_PREFIX=/usr \
  -DINSTALL_USER_UDEV_RULES=OFF \
  -DCMAKE_BUILD_TYPE=RelWithDebInfo \
  -DOPTIMIZE=native \
  -DQT6=OFF \
  -DBULK=ON \
  -DFAAD=ON \
  -DLILV=ON \
  -DFFMPEG=ON \
  -DKEYFINDER=ON \
  -DMAD=ON \
  -DMODPLUG=ON \
  -DOPUS=ON \
  -DQTKEYCHAIN=ON \
  -DWAVPACK=ON
}

build() {
  cmake --build "$srcdir/$_pkgname/build" --parallel "$(nproc)" --target $_pkgname
}

check() {
  cmake --build "$srcdir/$_pkgname/build" --parallel "$(nproc)" --target $_pkgname-test
  ctest --test-dir "$srcdir/$_pkgname/build" --parallel "$(nproc)" --output-on-failure
}

package() {
  mkdir -p "$pkgdir/usr/lib/udev/rules.d/"
  install -Dm644 "$srcdir/$_pkgname/res/linux/$_pkgname-usb-uaccess.rules" "$pkgdir/usr/lib/udev/rules.d/99-$_pkgname-usb-uaccess.rules"
  DESTDIR="$pkgdir" cmake --install "$srcdir/$_pkgname/build"
}
